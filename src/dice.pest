WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT    = _{ "[" ~ (!"]" ~ ANY)* ~ "]" }

natural = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT{,3} }
dice    = ${ natural ~ ^"d" ~ natural }
primary = _{ dice | natural | "(" ~ expr ~ ")" }
atom    = _{ unary_op? ~ primary }

unary_op    = _{ unary_minus }
unary_minus =  { "-" }

bin_op   = _{ add | subtract | multiply | divide }
add      =  { "+" }
subtract =  { "-" }
multiply =  { "*" }
divide   =  { "/" }

expr     =  { atom ~ (bin_op ~ atom)* }
equation = _{ SOI ~ expr ~ EOI }
